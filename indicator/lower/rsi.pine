// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © odnac

//@version=6
indicator("lower")
display_Option = display.all - display.status_line

showRSI = input.bool(false, 'show RSI')

// ------------------------------------
// RSI
// ------------------------------------
rsiLen = input.int(title="Period", minval=1, defval=14, group = 'RSI', display = display_Option)
rsiSrc = input(title="Source", defval=close, group = 'RSI', display = display_Option)
rsiLbR = input(title="Pivot Lookback Right", defval=5, display = display.data_window, group = 'RSI')
rsiLbL = input(title="Pivot Lookback Left", defval=5, display = display.data_window, group = 'RSI')
rsiRangeUpper = input(title="Max of Lookback Range", defval=60, display = display.data_window, group = 'RSI')
rsiRangeLower = input(title="Min of Lookback Range", defval=5, display = display.data_window, group = 'RSI')
rsiBearColor = color.red
rsiBullColor = color.green
rsiTextColor = color.white
rsiNoneColor = color.new(color.white, 100)
rsiOsc = ta.rsi(rsiSrc, rsiLen)

plot(showRSI ? rsiOsc : na, title="rsi", linewidth=1, color=color.rgb(41, 98, 255))
rsiObLevel = hline(showRSI ? 70 : na, title="Overbought", color=#787B86, linestyle=hline.style_dotted)
rsiOsLevel = hline(showRSI ? 30 : na, title="Oversold", color=#787B86, linestyle=hline.style_dotted)

rsiPlFound = na(ta.pivotlow(rsiOsc, rsiLbL, rsiLbR)) ? false : true
rsiPhFound = na(ta.pivothigh(rsiOsc, rsiLbL, rsiLbR)) ? false : true
_rsiInRange(cond) =>
	bars = ta.barssince(cond == true)
	rsiRangeLower <= bars and bars <= rsiRangeUpper

//------------------------------------------------------------------------------
// Regular Bullish
// Osc: Higher Low
rsiInRangePl = _rsiInRange(rsiPlFound[1])
rsiOscHL = rsiOsc[rsiLbR] > ta.valuewhen(rsiPlFound, rsiOsc[rsiLbR], 1) and rsiInRangePl

rsiPriceLL = low[rsiLbR] < ta.valuewhen(rsiPlFound, low[rsiLbR], 1)
rsiBullCond = rsiPriceLL and rsiOscHL and rsiPlFound

plot(showRSI ? rsiPlFound ? rsiOsc[rsiLbR] : na : na , offset=-rsiLbR, title="Regular Bullish", color=(rsiBullCond ? rsiBullColor : rsiNoneColor), display = display.pane)
plotshape(showRSI ? rsiBullCond ? rsiOsc[rsiLbR] : na : na, offset=-rsiLbR, title="Regular Bullish Label", style=shape.circle, location=location.absolute, color=rsiBullColor, textcolor=rsiTextColor)

//------------------------------------------------------------------------------
// Regular Bearish
// Osc: Lower High
rsiInRangePh = _rsiInRange(rsiPhFound[1])
rsiOscLH = rsiOsc[rsiLbR] < ta.valuewhen(rsiPhFound, rsiOsc[rsiLbR], 1) and rsiInRangePh

rsiPriceHH = high[rsiLbR] > ta.valuewhen(rsiPhFound, high[rsiLbR], 1)

rsiBearCond = rsiPriceHH and rsiOscLH and rsiPhFound

plot(showRSI ? rsiPhFound ? rsiOsc[rsiLbR] : na : na, offset=-rsiLbR, title="Regular Bearish", color=(rsiBearCond ? rsiBearColor : rsiNoneColor), display = display.pane)
plotshape(showRSI ? rsiBearCond ? rsiOsc[rsiLbR] : na : na, offset=-rsiLbR, title="Regular Bearish Label", style=shape.circle, location=location.absolute, color=rsiBearColor, textcolor=rsiTextColor)